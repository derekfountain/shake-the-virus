#!/usr/bin/wish

set ::NUM_BOIDS 30
set ::SPEED 20

set c [canvas .c -width 256 -height 192]
pack $c
focus $c

expr {srand(0)}
array set dots {}
for { set i 0 } { $i < $::NUM_BOIDS } { incr i } {
    set dots($i,graphic) [$c create rectangle 0 0 1 1]
    set dots($i,velocity) [list 0 0]

    set dots($i,position) [list [expr {int(256*rand())}] [expr {int(192*rand())}]]
    set dots($i,position_int) "[expr {round([lindex $dots($i,position) 0])}] [expr {round([lindex $dots($i,position) 1])}]"
}
set centre [$c create rectangle 0 0 1 1 -outline yellow]
set goal   [$c create rectangle 0 0 5 5 -outline red]

set goal_x 128
set goal_y 96

bind $c <KeyPress-q> {incr ::goal_y -4; puts "Goal $::goal_x , $::goal_y"}
bind $c <KeyPress-a> {incr ::goal_y  4; puts "Goal $::goal_x , $::goal_y"}
bind $c <KeyPress-o> {incr ::goal_x -4; puts "Goal $::goal_x , $::goal_y"}
bind $c <KeyPress-p> {incr ::goal_x  4; puts "Goal $::goal_x , $::goal_y"}

proc draw_dots {} {
    global dots

    for { set i 0 } { $i < $::NUM_BOIDS } { incr i } {
	$::c moveto $dots($i,graphic) [expr {[lindex $dots($i,position) 0]}] \
	                              [expr {[lindex $dots($i,position) 1]}]
    }
}

proc vector_distance { v1 v2 } {
    set x_sq [expr { ([lindex $v1 0]-[lindex $v2 0]) * ([lindex $v1 0]-[lindex $v2 0]) } ]
    set y_sq [expr { ([lindex $v1 1]-[lindex $v2 1]) * ([lindex $v1 1]-[lindex $v2 1]) } ]
    return [expr {(sqrt($x_sq + $y_sq))}]
}

proc vector_add { v1 v2 } {
    set r [list [expr {[lindex $v1 0]+[lindex $v2 0]}] [expr {[lindex $v1 1]+[lindex $v2 1]}] ]
    return $r
}

proc vector_sub { v1 v2 } {
    set r [list [expr {[lindex $v1 0]-[lindex $v2 0]}] [expr {[lindex $v1 1]-[lindex $v2 1]}] ]
    return $r
}

proc vector_mul { v1 m } {
    set r [list [expr {[lindex $v1 0]*$m}] [expr {[lindex $v1 1]*$m}] ]
    return $r
}

proc vector_div { v1 d } {
    set r [list [expr {([lindex $v1 0]/$d)}] [expr {([lindex $v1 1]/$d)}] ]
    return $r
}

proc vector_div_rounded { v1 d } {
    set r [list [expr {round([lindex $v1 0]/$d)}] [expr {round([lindex $v1 1]/$d)}] ]
    return $r
}

# Original algorithm calculates the centre of mass each time a boid is moved.
# It turns out that calculating this once, then applying the result as the
# location they all fly towards, still looks ok. And it can be done with
# integer values.
#
proc precalculate_centre_of_mass {} {
    global dots
    set calculated_centre {0 0}

    # Calc centre based on integer positions
    for { set i 0 } { $i < $::NUM_BOIDS } { incr i } {
	set calculated_centre [vector_add $calculated_centre $dots($i,position_int)]
    }
    set centre_of_mass [vector_div_rounded $calculated_centre $::NUM_BOIDS]

    return $centre_of_mass
}

# Rule 2: Boids try to keep a small distance away from other boids
#
proc avoid_others { index } {
    global dots

    set MIN_DISTANCE 4
    set boid_velocity [list 0 0]

    for { set i 0 } { $i < $::NUM_BOIDS } { incr i } {
        if { $i != $index } {
            set distance [vector_distance $dots($i,position) $dots($index,position)]
            if { $distance < $MIN_DISTANCE } {
                set boid_velocity [vector_sub $boid_velocity [vector_sub $dots($i,position) $dots($index,position)]]
            }
        }
    }

    return $boid_velocity
}

# Rule 3: Boids try to match velocity with near boids. This
# appears to boost the "milling around" behaviour, but I'm
# not sure how. I think the fast ones are imparting part of
# their speed on the slower ones.
#
# It turns out this isn't really necessary. All we need to 
# do is give a bump to a stationary boid.
#
proc bump_stationary { index } {
    global dots

    # Give a stationary one a nudge
    if { $dots($index,velocity) eq {0 0} } {
	return "[expr rand()/16] [expr rand()/16]"
    }
    return {0 0}
}


# Restrict how fast the boids can move
#
proc limit_velocity { index } {
    global dots
    
    set SPEED_LIMIT 2

    set magnitude [vector_distance {0 0} $dots($index,velocity)]
    if { $magnitude > $SPEED_LIMIT } {
	set t [vector_div $dots($index,velocity) $magnitude]
	set dots($index,velocity) [vector_mul $t $SPEED_LIMIT]
    }
}

proc move_all_dots {} {
    global dots

    for { set i 0 } { $i < $::NUM_BOIDS } { incr i } {

	set v2 [avoid_others      $i]
	set v3 [bump_stationary   $i]
    
	# Extra rule to make the boids follow the red dot.
	# Move 1% closer to it each iteration
	#
	set v4 [vector_sub [list $::goal_x $::goal_y] $dots($i,position)]
	set v4 [vector_div $v4 100]

	set dots($i,velocity) [vector_add $dots($i,velocity) $v2]
	set dots($i,velocity) [vector_add $dots($i,velocity) $v3]
	set dots($i,velocity) [vector_add $dots($i,velocity) $v4]

	limit_velocity $i
	set dots($i,position) [vector_add $dots($i,position) $dots($i,velocity)]
	set dots($i,position_int) "[expr {round([lindex $dots($i,position) 0])}] [expr {round([lindex $dots($i,position) 1])}]"
    }

    draw_dots
    $::c moveto $::goal $::goal_x $::goal_y

    # Test: Report position and velocity
    #
    for { set i 0 } { $i < $::NUM_BOIDS } { incr i } {
    	puts "$i : Pos $dots($i,position), Vel $dots($i,velocity)"
    }

    after $::SPEED move_all_dots
}


after $::SPEED move_all_dots
