#!/usr/bin/wish

set ::NUM_BOIDS 30
set ::SPEED 20

set c [canvas .c -width 256 -height 192]
pack $c
focus $c

expr {srand(0)}
array set dots {}
for { set i 0 } { $i < $::NUM_BOIDS } { incr i } {
    set dots($i,graphic) [$c create rectangle 0 0 1 1]
    set dots($i,velocity) [list 0 0]

    set dots($i,position) [list [expr {int(256*rand())}] [expr {int(192*rand())}]]
    set dots($i,position_int) "[expr {round([lindex $dots($i,position) 0])}] [expr {round([lindex $dots($i,position) 1])}]"
}
set centre [$c create rectangle 0 0 1 1 -outline yellow]
set goal   [$c create rectangle 0 0 5 5 -outline red]

set goal_x 128
set goal_y 96

bind $c <KeyPress-q> {incr ::goal_y -6; puts "Goal $::goal_x , $::goal_y"}
bind $c <KeyPress-a> {incr ::goal_y  6; puts "Goal $::goal_x , $::goal_y"}
bind $c <KeyPress-o> {incr ::goal_x -6; puts "Goal $::goal_x , $::goal_y"}
bind $c <KeyPress-p> {incr ::goal_x  6; puts "Goal $::goal_x , $::goal_y"}

proc draw_dots {} {
    global dots

    for { set i 0 } { $i < $::NUM_BOIDS } { incr i } {
	$::c moveto $dots($i,graphic) [expr {[lindex $dots($i,position) 0]}] \
	                              [expr {[lindex $dots($i,position) 1]}]
    }
}

proc vector_distance { v1 v2 } {
    set x_sq [expr { ([lindex $v1 0]-[lindex $v2 0]) * ([lindex $v1 0]-[lindex $v2 0]) } ]
    set y_sq [expr { ([lindex $v1 1]-[lindex $v2 1]) * ([lindex $v1 1]-[lindex $v2 1]) } ]
    return [expr {round(sqrt($x_sq + $y_sq))}]
}

proc vector_distance_no_sqrt { v1 v2 } {
    set x_sq [expr { ([lindex $v1 0]-[lindex $v2 0]) * ([lindex $v1 0]-[lindex $v2 0]) } ]
    set y_sq [expr { ([lindex $v1 1]-[lindex $v2 1]) * ([lindex $v1 1]-[lindex $v2 1]) } ]
    return [expr {$x_sq + $y_sq}]
}

proc vector_add { v1 v2 } {
    set r [list [expr {[lindex $v1 0]+[lindex $v2 0]}] [expr {[lindex $v1 1]+[lindex $v2 1]}] ]
    return $r
}

proc vector_sub { v1 v2 } {
    set r [list [expr {[lindex $v1 0]-[lindex $v2 0]}] [expr {[lindex $v1 1]-[lindex $v2 1]}] ]
    return $r
}

proc vector_mul { v1 m } {
    set r [list [expr {[lindex $v1 0]*$m}] [expr {[lindex $v1 1]*$m}] ]
    return $r
}

proc vector_div { v1 d } {
    set r [list [expr {([lindex $v1 0]/$d)}] [expr {([lindex $v1 1]/$d)}] ]
    return $r
}

proc vector_div_rounded { v1 d } {
    set r [list [expr {round([lindex $v1 0]/$d)}] [expr {round([lindex $v1 1]/$d)}] ]
    return $r
}

# Rule 2: Boids try to keep a small distance away from other boids
#
proc avoid_others { index } {
    global dots

    set MIN_DISTANCE 4
    set boid_velocity [list 0 0]

    # This can be done with integer positioning
    #
    for { set i 0 } { $i < $::NUM_BOIDS } { incr i } {
        if { $i != $index } {
            set distance [vector_distance_no_sqrt $dots($i,position_int) $dots($index,position_int)]
            if { $distance < $MIN_DISTANCE } {
                set boid_velocity [vector_sub $boid_velocity [vector_sub $dots($i,position_int) $dots($index,position_int)]]
            }
        }
    }

    return $boid_velocity
}

# Rule 3: Give a bump to a stationary boid.
#
proc bump_stationary { index } {
    global dots

    # Give a stationary one a nudge
    if { $dots($index,velocity) eq {0 0} } {
	return "[expr rand()/16] [expr rand()/16]"
    }
    return {0 0}
}


# Restrict how fast the boids can move
#
proc limit_velocity { index } {
    global dots
    
    set SPEED_LIMIT 8

    set magnitude [vector_distance_no_sqrt {0 0} $dots($index,velocity)]
    if { $magnitude > $SPEED_LIMIT } {
	set t [vector_div $dots($index,velocity) $magnitude]
	set dots($index,velocity) [vector_mul $t $SPEED_LIMIT]
    }
}

proc move_all_dots {} {
    global dots

    for { set i 0 } { $i < $::NUM_BOIDS } { incr i } {

	set v2 [avoid_others      $i]
	set v3 [bump_stationary   $i]
    
	# Extra rule to make the boids follow the red dot.
	# Move 1% closer to it each iteration
	#
# no int
# no rounded (probably)
# This is very cheap, so leave it alone for now
	set v4 [vector_sub [list $::goal_x $::goal_y] $dots($i,position)]
	set v4 [vector_div $v4 100]

	set dots($i,velocity) [vector_add $dots($i,velocity) $v2]
	set dots($i,velocity) [vector_add $dots($i,velocity) $v3]
	set dots($i,velocity) [vector_add $dots($i,velocity) $v4]

# no sqrt in this one
	limit_velocity $i

	set dots($i,position) [vector_add $dots($i,position) $dots($i,velocity)]
	set dots($i,position_int) "[expr {round([lindex $dots($i,position) 0])}] [expr {round([lindex $dots($i,position) 1])}]"
    }

    draw_dots
    $::c moveto $::goal $::goal_x $::goal_y

    # Test: Report position and velocity. half16 package on Spectrum is limited to 65535(ish)
    # so flag up if we go outside that range.
    #
    for { set i 0 } { $i < $::NUM_BOIDS } { incr i } {
	if { [lindex $dots($i,position) 0] < -65000 || [lindex $dots($i,position) 0] > 65000
	     ||
	     [lindex $dots($i,position) 1] < -65000 || [lindex $dots($i,position) 1] > 65000
	     ||
	     [lindex $dots($i,velocity) 0] < -65000 || [lindex $dots($i,velocity) 0] > 65000
	     ||
	     [lindex $dots($i,velocity) 1] < -65000 || [lindex $dots($i,velocity) 1] > 65000
	 } {
	    puts "$i : Pos $dots($i,position), Vel $dots($i,velocity)"
	    puts "$i : Pos $dots($i,position_int), Vel $dots($i,velocity)"
	}
    }

    after $::SPEED move_all_dots
}


after $::SPEED move_all_dots
